plugins {
  id "org.jetbrains.kotlin.jvm" version "1.4.30"
  id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
  id "net.thauvin.erik.gradle.semver" version "1.0.4"
  id 'maven-publish'
  id 'java'
}

group = 'no.ok.origo.dataplatform'

repositories {
  mavenCentral()
  jcenter()
}

test {
  environment "SERVICE_NAME", "some-service"
  useJUnitPlatform()
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.+'
  implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.+"
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
  implementation 'com.github.kittinunf.fuel:fuel:2.2.0'
  implementation 'com.github.kittinunf.fuel:fuel-jackson:2.2.0'

  implementation 'com.amazonaws:aws-lambda-java-core:1.1.0'

  testImplementation "io.mockk:mockk:1.10.+"
  testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
  testImplementation "com.github.tomakehurst:wiremock-jre8:2.25.1"
  testImplementation group: 'uk.org.lidalia', name: 'slf4j-test', version: '1.2.0'
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

publishing {
  publications{
    commonJvm(MavenPublication){
      from components.java
      artifactId = 'common-jvm'
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
      }
    }
  }
  repositories {
    maven {
      name = "NexusOK"
      url = "https://artifacts.oslo.kommune.no/repository/maven-releases"
    }
  }
}

build {
  dependsOn ktlintMainSourceSetCheck
  dependsOn test
}

java {
  withSourcesJar()
  withJavadocJar()
}

generateMetadataFileForCommonJvmPublication {
  dependsOn {
    incrementPatch
  }
}